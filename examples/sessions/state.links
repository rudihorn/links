typename State(a) = AP (!a.End);

sig newCell : forall a::Any.(a) ~> State(a)
fun newCell(v) {
  var x = new();
  var _ = spawn {send(v, accept(x))};
  x
}

sig put : (State(a), a) ~> ()
fun put(x, v) {
  var _ = receive(request(x)).1;
  var _ = spawn {send(v, accept(x))};
  ()
}

sig get : (State(a)) ~> a
fun get(x) {
  var v = receive(request(x)).1;
  var _ = spawn {send(v, accept(x))};
  v
}

# an atomic swap operation
sig swap : forall a::Any.(State(a), a) ~> a
fun swap(x, w) {
  var v = receive(request(x)).1;
  var _ = spawn {send(w, accept(x))};
  v
}

# Landin's knot - this function loops forever
sig forever : () ~> ()
fun forever() {
  var x = newCell(fun () {()});
  put(x, fun () {get(x)()});
  get(x)()
}


## Landin's knot - this function loops forever
#sig forever0 : () ~> ()
#fun forever0() {
#  var x = newCell(linfun () {()});
#  put(x, linfun () {get(x)()});
#  get(x)()
#}


forever()
